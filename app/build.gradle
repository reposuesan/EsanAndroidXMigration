apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("delivery/credentials/prod.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    
    defaultConfig {
        applicationId "appostgrado.esan.edu.pe"
        minSdkVersion 21
        targetSdkVersion 30
        multiDexEnabled true
        //El versionCode actual del Play Store es 92
        versionCode 93
        //El versionName actual del Play Store es 5.0.47
        versionName "5.0.48"
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String","ESAN", propertyOrEmpty("ESAN"))
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //Default ABI list for this app, can be over-ridden by providing an abiList property
            //e.g. gradle -PabiList=x86 clean assembleDebug
            abiFilters((project.findProperty('abiList') ?: 'armeabi-v7a, arm64-v8a, x86').split(/,\s*/))
        }
    }

    archivesBaseName = rootProject.projectDir.getName()

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeF'])
            storePassword keystoreProperties['storeP']
            keyAlias keystoreProperties['keyA']
            keyPassword keystoreProperties['keyP']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }
    }

    flavorDimensions "version"
    productFlavors {
        esan {
            dimension "version"
        }

        esanvuf {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }
}

def propertyOrEmpty(String name) {
    String property = findProperty(name)
    if(property == "" || property == null) {
        return ""
    } else {
        return property
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Kotlin Reflect
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //Android Times Square
    implementation 'com.squareup:android-times-square:1.6.5@aar'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //Google Play Services Vision, Analytics, Maps
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    //DecoView charting
    implementation 'com.github.bmarrdev:android-DecoView-charting:v1.2'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    //RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'

    //Volley
    implementation 'com.android.volley:volley:1.2.0'

    //CardView
    implementation 'androidx.cardview:cardview:1.0.0'

    //AppCompat v7, Design, Support Media Compat, Support v4
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.media:media:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //Multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    //UNCOMMENT THIS TO ENABLE VUFORIA IN THE PROJECT
    implementation project(path: ':vuforiamod')

    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    def room_version = "2.3.0"
    def coroutines_android_version = '1.3.9'
    def lifecycle_version = '2.2.0'
    def livedata_version = '2.2.0-rc02'

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"

    //ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    //Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    //ENABLE LEAKCANARY FOR DEBUG ONLY (debugImplementation because LeakCanary should only run in debug builds)
    /*debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'*/

    //QR Code Generator library
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    //Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}


